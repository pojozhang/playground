buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

apply from: 'docker/docker.gradle'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
}

ext {
    lombokVersion = '1.18.20'
    junitVersion = '5.7.2'
    springBootVersion = '2.5.1'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly(
            "org.projectlombok:lombok:$lombokVersion",
    )

    implementation(
            'org.jetbrains:annotations:21.0.1',
            'org.apache.commons:commons-lang3:3.12.0',
            'commons-io:commons-io:2.10.0',
            'com.google.guava:guava:30.1.1-jre',
            'com.alibaba:fastjson:1.2.76',
            'cglib:cglib-full:2.0.2',
            'com.rabbitmq:amqp-client:5.12.0',
            'com.rabbitmq:http-client:3.10.0',
            'org.apache.kafka:kafka-clients:2.8.0',
            'org.apache.httpcomponents:httpclient:4.5.13',
            'io.lettuce:lettuce-core:6.1.3.RELEASE',
            'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.13.3',
            "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-aop:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion",
    )

    implementation('org.apache.zookeeper:zookeeper:3.7.0') {
        exclude module: 'slf4j-log4j12'
    }

    runtimeOnly(
            'com.h2database:h2:1.4.200',
    )

    testImplementation(
            'org.assertj:assertj-core:3.20.2',
            'org.awaitility:awaitility:4.1.0',
            'org.junit.platform:junit-platform-launcher:1.7.2',
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.vintage:junit-vintage-engine:$junitVersion",
            "org.springframework.boot:spring-boot-starter-test:$springBootVersion",
    )

    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
    )
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
}