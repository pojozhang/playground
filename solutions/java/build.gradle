buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public/' }
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

apply from: 'docker/docker.gradle'

repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

test {
    useJUnitPlatform()
}

ext {
    lombokVersion = '1.18.38'
    junitVersion = '5.13.0'
    springBootVersion = '3.5.0'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly(
            "org.projectlombok:lombok:$lombokVersion",
    )

    implementation(
            'javax.persistence:javax.persistence-api:2.2',
            'org.jetbrains:annotations:26.0.2',
            'org.apache.commons:commons-lang3:3.17.0',
            'commons-io:commons-io:2.19.0',
            'com.google.guava:guava:33.4.8-jre',
            'com.alibaba:fastjson:2.0.57',
            'cglib:cglib-full:2.0.2',
            'com.rabbitmq:amqp-client:5.25.0',
            'com.rabbitmq:http-client:5.3.0',
            'org.apache.kafka:kafka-clients:4.0.0',
            "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-aop:$springBootVersion",
            "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion",
    )

    implementation('org.apache.zookeeper:zookeeper:3.9.3') {
        exclude module: 'slf4j-log4j12'
    }

    runtimeOnly(
            'com.h2database:h2:2.3.232',
    )

    testImplementation(
            'io.lettuce:lettuce-core:6.6.0.RELEASE',
            'org.assertj:assertj-core:3.27.3',
            'org.awaitility:awaitility:4.3.0',
            'org.junit.platform:junit-platform-launcher:1.13.0',
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.vintage:junit-vintage-engine:$junitVersion",
            "org.springframework.boot:spring-boot-starter-test:$springBootVersion",
    )

    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
    )
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
}